buildscript {
    ext.kotlin_version = '1.2.31'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "de.undercouch.download" version "3.2.0"
}
apply plugin: 'kotlin'
apply plugin: 'antlr'

group 'de.hhu.stups'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/kotlin', 'build/generated-src']
        }
    }
}

dependencies {
    antlr "org.antlr:antlr4:4.7"
    compile files('alloy4.2.jar')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile 'junit:junit:4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages", "-package", "de.hhu.stups.alloy2b.antlr"]
    outputDirectory = file('build/generated-src/de/hhu/stups/alloy2b/antlr')
}
compileJava.dependsOn generateGrammarSource
compileKotlin.dependsOn generateGrammarSource

sourceSets {
    generated {
        java.srcDir 'build/generated-src/'
    }
}
compileJava.source sourceSets.generated.java, sourceSets.main.java

jar {
    manifest {
        attributes 'Main-Class': 'de.hhu.stups.alloy2b.Alloy2BKt'
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task downloadAlloy {
    doFirst {
        download {
            src 'http://alloy.mit.edu/alloy/downloads/alloy4.2.jar'
            dest 'alloy4.2.jar'
            onlyIfNewer true
            overwrite false
        }
    }
}

compileKotlin {
    dependsOn {
        downloadAlloy
    }
}

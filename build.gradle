buildscript {
    ext.kotlin_version = '1.7.21'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'de.hhu.stups'
archivesBaseName = 'alloy2b'
version = '1.3-SNAPSHOT'
final isSnapshot = project.version.endsWith("-SNAPSHOT")
mainClassName = 'de.hhu.stups.alloy2b.Alloy2BKt'

wrapper {
    gradleVersion = "7.4.2"
}

repositories {
    mavenCentral()
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
    test.java.srcDirs += 'src/test/kotlin/'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "de.hhu.stups:alloy-core:5.1.0"
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.9.0')
}

java {
    withSourcesJar()
    withJavadocJar()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes(
                'Main-Class': mainClassName,
                'version': project.version, // FIXME What is this used by?
        )
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    exclude('META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA')
}

sourcesJar {
    manifest {
        // FIXME Does the sources jar need these attributes?
        attributes(
            'Main-Class': mainClassName,
            'version': project.version,
        )
    }
}

javadoc {
    failOnError = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'alloy2b'
                description = 'The Parser for our translation from Alloy to B returning a Prolog Term.'
                url = 'https://github.com/hhu-stups/alloy2b'

                licenses {
                    license {
                        name = 'Eclipse Public License, Version 1.0'
                        url = 'https://www.eclipse.org/org/documents/epl-v10.html'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/hhu-stups/alloy2b.git'
                    developerConnection = 'scm:git:git@github.com:hhu-stups/alloy2b.git'
                    url = 'https://github.com/hhu-stups/alloy2b'
                }

                developers {
                    developer {
                        id = 'JoshuaAtSTUPS'
                        name = 'Joshua Schmidt'
                        email = 'joshua.schmidt@hhu.de'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            final releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            final snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url isSnapshot ? snapshotsRepoUrl : releasesRepoUrl
            if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                credentials {
                    username project.ossrhUsername
                    password project.ossrhPassword
                }
            }
        }
    }
}

tasks.withType(Jar.class).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

signing {
    sign publishing.publications.mavenJava
}
